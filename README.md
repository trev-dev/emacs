# Emacs Configuration


To read more about how this configuration is supposed to work, see my [config strategy](#orgf1de1e2).

**Note:** *This document was generated using Emacs Org Mode by exporting [config.org](config.org).* The programs this document creates are also generated by Emacs Org Mode by tangling the same file.

All references this document makes to itself are referring to it's Orgdown counterpart.

**Table of Contents**

1.  [Configuration Strategy](#orgf1de1e2)
    1.  [Tangling Files](#org17ac471)
    2.  [Configuring Packages](#org8ea11cd)
2.  [Early Init](#orge2ea3cd)
3.  [Init File Headers](#org88d20bd)
4.  [Package Manifest](#orgb67a152)
5.  [Bootstrap Straight](#org49b81c9)
6.  [General Settings](#orgf4648c1)
7.  [Helper Functions](#orgb829983)
8.  [Priority Mode](#org0b08bef)
9.  [Keybinds](#org375e2fb)
10. [Prog Mode](#org8c1b07d)
11. [Look & Feel](#org3a37d90)
    1.  [Dired](#orgda5ba78)
    2.  [Custom Theme Devel](#org1ab54a6)
    3.  [Font Setup](#orgc8b2656)
12. [Modeline](#org7a79926)
    1.  [Git status](#orgff6a568)
    2.  [Project indicator](#orgb3043e1)
    3.  [Modal mode indicators](#orgef02b91)
    4.  [God mode indicator](#org2c130a3)
    5.  [Buffer related info](#org6d56fdc)
    6.  [Various other package indicators](#orgff7485d)
    7.  [Render a split-view mode-line](#orgcb5635f)
    8.  [Diminish the minor-mode-alist](#org6a8f038)
13. [Utility Packages](#org37a5b52)
    1.  [Anzu](#org7dc9ba9)
    2.  [Completions](#orgc5465d9)
    3.  [Docker](#orgeaf4657)
    4.  [Diff-hl](#org7600a95)
    5.  [Ediff](#orgd059f66)
    6.  [Elfeed](#org25c0822)
    7.  [EMMS](#orgeed92b9)
    8.  [Surround](#org76a51be)
    9.  [Ement](#org23ab139)
    10. [ERC](#org0f0f439)
    11. [Eshell](#org4eda760)
    12. [Evil](#orgc04d505)
    13. [Expand Region](#orgd97d941)
    14. [Imenu](#org8d1baf7)
    15. [Magit](#org46abf74)
    16. [Org](#orgafbf924)
    17. [Ledger](#org69e45d4)
    18. [Notmuch](#orgf777ad5)
    19. [Password Store](#orgc64ec82)
    20. [Sensitive Mode](#org1c19d34)
    21. [RG](#orgcf20fa4)
    22. [Tree-Sitter](#orgf3e1834)
    23. [Visual Fill Column](#org83af6eb)
    24. [Which-key](#org5cd7df9)
    25. [Windmove](#org99aa060)
14. [Syntax Support](#orgb7ba7a9)
    1.  [Clojure](#orgaa22957)
    2.  [Lisp](#orgfd0363d)
    3.  [CSS/SCSS](#orga679efd)
    4.  [Emmet](#org570ebcb)
    5.  [Flycheck](#org00fb25e)
    6.  [LSP Mode](#org40c7bbe)
    7.  [Lispy](#org2b19dc8)
    8.  [Markdown](#org9bea7ad)
    9.  [Nix Mode](#orgfd2d7fc)
    10. [PHP](#org40a8652)
    11. [Prettier](#orgff5a920)
    12. [Python](#org8de346d)
    13. [Rainbow Delimiters](#org3cd2e08)
    14. [Rainbow Mode](#org7496449)
    15. [Ruby](#org060231a)
    16. [Rust](#orge8907ab)
    17. [Shopify Liquid](#orgfa1e89f)
    18. [Svelte](#org8f70b95)
    19. [TypeScript & JavaScript](#orgc6805d1)
    20. [VueJS](#orgf046ae3)
    21. [Web Mode](#org8df1527)
    22. [YAML](#orgfd8a4a7)
    23. [Yasnippet](#org2e120c6)
    24. [Load Customizer Settings](#org9dea35e)
15. [Installation](#orgc0c7a1c)
16. [Licenses](#org9bdc172)
17. [Closing Thoughts](#org24d4bcf)


<a id="orgf1de1e2"></a>

## Configuration Strategy

The goal of this config is to have a decently reproducible configuration with the help of straight.el. Once packages are installed, start-up time is minimized so that you're up and running quick, be that in daemon mode or not.


<a id="org17ac471"></a>

### Tangling Files

This file is more than documentation. It is a literate program. You may use it to tangle the `./.emacs.d/` directory. From there you may symlink that folder into your home directory or do whatever with it.


<a id="org8ea11cd"></a>

### Configuring Packages

Each package gets its own headline. In that headline is a noweb tangle block for pre-processing the [package manifest](#orgb67a152). The second block is the actual, plain configuration. Things may then be carried out in order so that packages are never missing when configuration occurs.


<a id="orge2ea3cd"></a>

## Early Init

This file is loaded before the initialization of emacs begins. It is sometimes helpful to pre-configure stuff in here.

**Note:** The `:PROPERTIES:` drawer in this section flags org-babel to make sure that the `.emacs.d/` directory exists using the `:mkdirp yes` argument.

```elisp
;;; early-init.el --- Emacs early-init setup.
;;
;;; Commentary:
;; This file was generated using literate programming. Please see the config.org
;; file.
;;
;;; Code:
```

We want the garbage collector to have no limit during the init sequence.

```elisp
(setq gc-cons-threshold most-positive-fixnum)
(setq package-enable-at-startup nil)
```

```elisp
;;; early-init.el ends here
```


<a id="org88d20bd"></a>

## Init File Headers

This generates the top of the init file, which will set up the lexical scope and describe to Emacs what the file does.

```elisp
;;; init.el --- Trev's Emacs config -*- lexical-binding: t -*-
;;
;; Copyright (C) 2022 Trevor Richards
;;
;; Author: Trevor Richards <trev@trevdev.ca>
;; Maintainer: Trevor Richards <trev@trevdev.ca>
;; URL: https://github.com/trev-dev/emacs
;; Created: 2nd August, 2022
;; Version: 1.0.0
;; License: GPL3
;;
;; This file is not a part of GNU Emacs.
;;
;; This program is free software: you can redistribute it and/or modify it under
;; the terms of the GNU General Public License as published by the Free Software
;; Foundation, either version 3 of the License, or (at your option) any later
;; version.
;;
;; This program is distributed in the hope that it will be useful, but
;; WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
;; FITNESS FOR A PARTICULAR PURPOSE.
;;
;; See the GNU General Public License for more details. You should have received
;; a copy of the GNU General Public License along with this program. If not, see
;; <https://www.gnu.org/licenses/>.
;;
;;; Commentary:
;; This program was generated using literate programming. See config.org for
;; details and documentation.
;;
;;; Code:
```


<a id="orgb67a152"></a>

## Package Manifest

```elisp
(defvar package-manifest
  (list
   'all-the-icons
   'all-the-icons-dired
   'svg-lib
   'diminish
   'anzu
   'company
   'orderless
   'docker
   'dockerfile-mode
   'docker-compose-mode
   'diff-hl
   'elfeed
   'emms
   'ement
   'eshell-syntax-highlighting
   'evil
   'evil-lion
   'evil-matchit
   'evil-surround
   'evil-snipe
   'evil-exchange
   'evil-goggles
   'evil-multiedit
   'evil-commentary
   'evil-collection
   'evil-org
   'expand-region
   'magit
   'org
   'org-chef
   'ox-gfm
   'ox-hugo
   'org-present
   'org-roam
   'org-roam-ui
   'ledger-mode
   'notmuch
   'ol-notmuch
   'org-mime
   'org-contacts
   'password-store
   'rg
   'tree-sitter
   'tree-sitter-langs
   'visual-fill-column
   'which-key
   'clojure-mode
   'cider
   'sly
   'geiser-guile
   'emmet-mode
   'flycheck
   'lsp-mode
   'lsp-ui
   'lispy
   'markdown-mode
   'nix-mode
   'php-mode
   'prettier
   'pyvenv
   'rainbow-delimiters
   'rainbow-mode
   'inf-ruby
   'rust-mode
   'typescript-mode
   'web-mode
   'yaml-mode
   'yasnippet
   'yasnippet-snippets)
  "The complete list of straight packages")
```


<a id="org49b81c9"></a>

## Bootstrap Straight

Straight.el opens up the whole Internet of Emacs packages. It's good for developing packages. If you want easy access to everything out there, straight is the way to go.

```elisp
(setq straight-check-for-modifications '(check-on-save find-when-checking))

(defvar bootstrap-version)
(let ((bootstrap-file
       (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))
      (bootstrap-version 6))
  (unless (file-exists-p bootstrap-file)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/radian-software/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp)))
  (load bootstrap-file nil 'nomessage))

(dolist (pkg package-manifest)
  (straight-use-package pkg))
```


<a id="orgf4648c1"></a>

## General Settings

Some general performance based improvements concerning large files, when to compile with the fancy new native-comp feature and reset the GC collection size after init.

```elisp
(defvar emacs-startup-time (current-time)
  "When Emacs last initialized.")

(defun td/calculate-init-time (start-time)
  (float-time (time-subtract (current-time) start-time)))

(defun td/report-init-time ()
  "Prints the init time into the scratch buffer as a comment."
  (switch-to-buffer "*scratch*")
  (insert-before-markers
   (format ";; Init finished in %0.2f seconds; welcome to Emacs.\n"
           (td/calculate-init-time emacs-startup-time))))

(add-hook 'after-init-hook #'td/report-init-time)

(add-hook 'emacs-startup-hook
          (lambda ()
            (setq gc-cons-threshold (expt 2 23))))

(setq read-process-output-max (* 1024 1024))
(global-so-long-mode 1)
(setq comp-deferred-compilation t)
```

Clear out most of the GUI clutter, display relative line numbers, highlight the line I'm on, smaller left-only fringe, quick yes/no answers, some prog-mode QOL settings as well.

```elisp
;; Interface
(scroll-bar-mode -1)
(tool-bar-mode -1)
(menu-bar-mode -1)
(blink-cursor-mode 1)
(setq display-line-numbers-type 'relative
      inhibit-startup-screen t
      initial-scratch-message ""
      history-length 25
      sentence-end-double-space nil
      visible-bell t)
(global-visual-line-mode 1)
(recentf-mode 1)
(save-place-mode 1)
(global-auto-revert-mode 1)
(fringe-mode '(4 . 0))
(defalias 'yes-or-no-p 'y-or-n-p)
(setq confirm-kill-emacs 'y-or-n-p)
```

Set up the dictionary and preferred browser.

```elisp
;; Spelling
(setq ispell-personal-dictionary "~/.config/emacs/personal-dict.pwd"
      ispell-dictionary "en"
      ispell-program-name "aspell"
      ispell-alternate-dictionary (concat (getenv "HOME") "/Documents/wordlist"))

(define-key text-mode-map (kbd "C-c f") #'flyspell-mode)

;; Browser
(setq browse-url-generic-program "/usr/bin/firefox")
```

I hate seeing project folders get all cluttered up. Let's move autosaves and backups somewhere else.

```elisp
;; Put autosave files (ie #foo#) and backup files (ie foo~) in ~/.emacs.d/.
(setq auto-save-file-name-transforms '((".*" "~/.emacs.d/autosaves/" t))
      backup-directory-alist '((".*" . "~/.emacs.d/backups/")))

;; create the autosave dir if necessary, since emacs won't.
(make-directory "~/.emacs.d/autosaves/" t)

(add-hook 'prog-mode-hook #'display-fill-column-indicator-mode)
```

Move custom variable settings to somewhere other that `init.el`.

```elisp
(setq custom-file (locate-user-emacs-file "custom-vars.el"))
```


<a id="orgb829983"></a>

## Helper Functions

Many people configure their emacs with the help of macros such as `use-package`. I prefer to keep my init lower-level and simple. I have written some simple functions that will make configuration less repetitive.

Using my own functions for the things I want or need keeps my overall package load smaller.

```elisp
(defun td/bind-keys (conses &optional mode-map)
  "Bind several keybinds using a list of `CONSES'.
Binds will be global unless the optional `MODE-MAP' is specified."
  (dolist (combo conses)
    (if (or (consp mode-map) (keymapp mode-map))
        (define-key mode-map (kbd (car combo)) (cdr combo))
      (if mode-map (warn "Optional %s `MODE-MAP' was invalid: %s" (type-of mode-map) mode-map))
      (global-set-key (kbd (car combo)) (cdr combo)))))

(defun td/add-hooks (modes func)
  "Set several hooks from a list of `CONSES'.
Adds '-hook' onto the end of the symbols for brevity."
  (dolist (mode modes)
    (add-hook (intern (concat (symbol-name mode) "-hook")) func)))

(defun td/auto-mode (modes)
  "Add the `MODES' to the `auto-mode-alist'."
  (dolist (mode modes)
    (add-to-list 'auto-mode-alist mode)))

(defun td/filter-nil (seq)
  "Filter out nil items from sequence `SEQ'."
  (seq-filter #'(lambda (item) item) seq))

(defun td/is-file-buffer (buffer)
  "Test if a buffer belongs to a file on the system. Returns non-nil if it does."
  (let ((file (buffer-file-name buffer)))
    (when file
      (file-exists-p file))))
```

Create a mode for mapping high priority keybinds early on.


<a id="org0b08bef"></a>

## Priority Mode

Sometimes 3rd party packages like to take over my keyboard with their own keybinds. There are some keybinds that I prefer to always have access to without accidently triggering someone else's code first, then having to undo whatever that did, and use `M-x`.

With Priority mode, I am creating an "emulation layer". This is similar to what some popular modal editing packages do (such as evil-mode). It makes sure that when my `priority-mode` is active, the keybinds assigned to it will always take priority over other minor-mode bindings.

```elisp
(define-minor-mode priority-mode
  "A minor mode for short-listing keybindings.
This will prevent other modes form overriding keys that I would prefer to
see bound."
  :init-value nil
  :global t
  :keymap (make-sparse-keymap))
(add-to-list 'emulation-mode-map-alists `((priority-mode . ,priority-mode-map)))
(priority-mode)
```


<a id="org375e2fb"></a>

## Keybinds

Change some of the built-in keybinds & bind some of the useful unbound functions.

```elisp
(defun td/forward-chunk ()
  (interactive)
  (next-line 20))

(defun td/backward-chunk ()
  (interactive)
  (previous-line 20))

(td/bind-keys '(("M-j" . join-line)
                ("M-n" . scroll-up-line)
                ("M-p" . scroll-down-line)))
```


<a id="org8c1b07d"></a>

## Prog Mode

A few settings that are useful in programming buffers that don't have much to do with 3rd party packages.

I am trying to respect the indent style of any file I come across, so I wrote some functions to help me with that.

```elisp
(defun td/toggle-indent-tabs-mode ()
  "Toggle `indent-tabs-mode'."
  (interactive)
  (setq-local indent-tabs-mode (not indent-tabs-mode)))

(defun td/infer-indentation-style ()
  "Figure out whether or not we are indenting with tabs or spaces.
Set `indent-tabs-mode' accordingly."
  (let ((space-count (how-many "^  "))
        (tab-count (how-many "^\t")))
    (if (> space-count tab-count)
        (setq indent-tabs-mode nil))
    (if (> tab-count space-count)
        (setq indent-tabs-mode t))))
```

I need a setup hook that will trigger when prog-mode is activated.

```elisp
(defun td/prog-mode-settings ()
  "A general set-up hook for prog-mode."
  (setq whitespace-style '(face tabs tab-mark trailing))
  (setq whitespace-display-mappings '((tab-mark 9 [9474 9] [92 9])))
  (custom-set-faces
   '(whitespace-tab ((t (:foreground "#636363")))))
  (setq-local fill-column 80)
  (setq-local show-trailing-whitespace t)
  (show-paren-mode t)
  (hs-minor-mode)
  (display-line-numbers-mode)
  (display-fill-column-indicator-mode)
  (electric-pair-local-mode)
  (td/infer-indentation-style)
  (whitespace-mode))
(add-hook 'prog-mode-hook 'td/prog-mode-settings)
```

I'd like to keep my tab style fixed at 2 spaces wherever possible. Specific programming modes can change this if they need to.

```elisp
(setq indent-tabs-mode nil)
(setq standard-indent 2)
(setq backward-delete-char-untabify-method 'hungry)
(setq-default indent-tabs-mode nil)
(setq-default tab-width 2)
(setq-default evil-shift-width 2)
(setq-default electric-indent-inhibit t)
```


<a id="org3a37d90"></a>

## Look & Feel


<a id="orgda5ba78"></a>

### Dired

I enjoy seeing icons next to file types. The `all-the-icons` and `all-the-icons-dired` package help facilitate this.

I also want to appropriate `gnus-dired-attach` for use with [notmuch](#orgcaca7d6).

```elisp
(setq dired-dwim-target t)
(with-eval-after-load 'dired
  (require 'all-the-icons)
  (setq all-the-icons-dired-monochrome nil)
  (add-hook
   'dired-mode-hook #'(lambda ()
                        (when (display-graphic-p)
                          (all-the-icons-dired-mode))
                        (dired-hide-details-mode)))

  (autoload 'gnus-dired-attach "gnus-dired.el"
    "Attach files from Dired to the current Message buffer." t)
  (define-key dired-mode-map (kbd "C-c C-m C-a") #'gnus-dired-attach))
```


<a id="org1ab54a6"></a>

### Custom Theme Devel

I've written my own theme called "tangonov".

```elisp
(add-to-list 'custom-theme-load-path "~/Projects/tangonov-theme/")
(setq tangonov-selection-foregrounds nil)
(defun td/load-theme (frame)
  "Load the theme correctly for a `FRAME' if we're using emacsclient."
  (select-frame frame)
  (load-theme 'tangonov t))

(if (daemonp)
    (add-hook 'after-make-frame-functions #'td/load-theme)
  (load-theme 'tangonov t))
```


<a id="orgc8b2656"></a>

### Font Setup

```elisp
(set-language-environment "UTF-8")
(set-default-coding-systems 'utf-8)
(add-to-list 'default-frame-alist '(font . "Hack 12"))
```


<a id="org7a79926"></a>

## Modeline

My custom modeline consists of many parts. The original goal was to create a light weight and simple modeline that displays the information that I want from a typical Emacs modeline in a way that I find appealing. I also hoped to never overflow in a 2 window view.

It has turned into something something else: A crazy exercise in learning how to optimize a modeline configuration that does silly things like render SVG icons.

The modeline config requires the `svg-lib` package.


<a id="orgff6a568"></a>

### Git status

With some effort I have been able to make a somewhat reliable, accurate status indicator for git projects that does not break my emacs.

```elisp
(defvar-local tdm/git-status-indicator ""
  "The buffer's last known workspace status.")

(put 'tdm/git-status-indicator 'risky-local-variable t)

(defvar tdm/git-status-plist '(unregistered ("  ⁈" . (:foreground "#C792EA"))
                               edited ("  ±" . (:foreground "#82AAFF"))
                               up-to-date ("  ✔" . success)))

(defun tdm/git-create-indicator (buffer)
  "Create a git status indicator using a `BUFFER'."
  (let* ((status (vc-state-refresh buffer 'git))
         (icon-and-color (plist-get tdm/git-status-plist status))
         (branch (cond ((eq status 'unregistered) "untracked")
                       ((or vc-mode (progn (vc-refresh-state) vc-mode))
                        (substring vc-mode 5))
                       (t ""))))
    (propertize
     (concat
      (car icon-and-color) " " branch)
     'face (cdr icon-and-color))))

(defun tdm/git-cache-status (&optional frame)
  "Set local buffer's git cache status.
When used as a window hook, receive the `FRAME' as an argument."
  (let ((proj (project-current))
        (buff (buffer-file-name)))
    (when (and proj (member buff (project-files proj)))
      (setq tdm/git-status-indicator (tdm/git-create-indicator buff)))))

(defun tdm/git-cache-status-post-magit ()
  (let ((start-buffer (current-buffer)))
    (dolist (buff (seq-filter #'td/is-file-buffer
                              (project-buffers (project-current))))
      (switch-to-buffer buff)
      (setq tdm/git-status-indicator (tdm/git-create-indicator
                                      (buffer-file-name buff))))
    (switch-to-buffer start-buffer)))

(add-hook 'after-save-hook #'tdm/git-cache-status)
(add-to-list 'window-buffer-change-functions #'tdm/git-cache-status)
(add-hook 'magit-post-refresh-hook #'tdm/git-cache-status-post-magit)
```


<a id="orgb3043e1"></a>

### Project indicator

```elisp
(defcustom tdm/custom-project-name nil
  "A custom directory-local name for a project.el project."
  :type 'string)

(defvar-local tdm/project-mode-line-name ""
  "Project the current buffer belogns to.")

(put 'tdm/project-mode-line-name 'risky-local-variable t)

(defun tdm/project ()
  "Display the current project name, or path."
  (let ((pc (project-current))
        (pname-not-set (string= tdm/project-mode-line-name "")))
    (if (and pc pname-not-set)
        (setq tdm/project-mode-line-name
              (format-mode-line
               (list
                `(:propertize ,(or tdm/custom-project-name
                                   (file-name-nondirectory
                                    (directory-file-name
                                     (project-root pc))))
                              face success
                              help-echo "Switch project"
                              mouse-face (:box 1)
                              local-map ,(make-mode-line-mouse-map
                                          'mouse-1 #'project-switch-project))
                '(:propertize ":" face (:inherit font-lock-comment-face)))))
      tdm/project-mode-line-name)))
```


<a id="orgef02b91"></a>

### Modal mode indicators

```elisp
(defun tdm/modal-face (str base)
  (propertize str 'face
              `(:inherit ,base :weight bold :height 0.9)))

(defvar tdm/custom-meow-states `((normal . ,(tdm/modal-face
                                             "<N>" '(:foreground "#FFCA41")))
                                 (motion . ,(tdm/modal-face
                                             "<M>" '(:foreground "#82AAFF")))
                                 (keypad . ,(tdm/modal-face
                                             "<K>" '(:foreground "#89DDFF")))
                                 (insert . ,(tdm/modal-face
                                             "<I>" '(:foreground "#C792EA")))
                                 (beacon . ,(tdm/modal-face
                                             "<B>" '(:foreground "#FF7B85")))))

(defvar tdm/evil-states `((normal . ,(tdm/modal-face
                                      "<N>" '(:foreground "#FFCA41")))
                          (motion . ,(tdm/modal-face
                                      "<M>" '(:foreground "#82AAFF")))
                          (operator . ,(tdm/modal-face
                                        "<O>" '(:foreground "#89DDFF")))
                          (insert . ,(tdm/modal-face
                                      "<I>" '(:foreground "#ABDC88")))
                          (visual . ,(tdm/modal-face
                                       "<V>" '(:foreground "#FF996B")))
                          (replace . ,(tdm/modal-face
                                       "<R>" '(:foreground "#FF7B85")))
                          (emacs . ,(tdm/modal-face
                                     "<E>" '(:foreground "#C792EA")))))

(defun tdm/meow-state ()
  "Retrieve the meow-state for the mode-line."
  (when (featurep 'meow)
    (concat (alist-get (meow--current-state) tdm/custom-meow-states)
            " ")))

(defun tdm/evil-state ()
  "Get the evil state for the mode-line."
  (when (featurep 'evil)
    (concat (alist-get evil-state tdm/evil-states)
            " ")))
```


<a id="org2c130a3"></a>

### God mode indicator

This is where we use `svg-lib` to bring the thunder.

```elisp
(defvar tdm/god-mode-icon
  (propertize "<G>" 'face '(:foreground "#FFCA41" :weight bold))
  "The hammer of the gods, but only if you are worthy.")

(defvar-local tdm/god-mode-indicator--cached ""
  "The cached state of the `god-local-mode' indicator for the mode-line.")

(put 'tdm/god-mode-indicator--cached 'risky-local-variable t)

(defun tdm/god-mode-icon--make-icon ()
  "Produce the hammer of the gods."
  (propertize
   "  " 'display (when (functionp 'svg-lib-icon)
                   (svg-lib-icon
                    "mjolnir"
                    `(:collection "local"
                      :stroke 0
                      :padding 0
                      :width 20
                      :foreground ,(face-foreground 'warning)
                      :background ,(face-background 'mode-line))))))

(defun tdm/god-mode-icon--set-icon ()
  "Set the `tdm/god-mode-icon' as an svg icon in graphical displays."
  (when (display-graphic-p)
    (setq tdm/god-mode-icon (tdm/god-mode-icon--make-icon))))

(defun tdm/god-mode-setup-icon ()
  "Set up the `tdm/god-mode-icon' as late as possible in the init process.

The reasoning for this is because if the window's not ready to go, an SVG
icon renders incorrectly."
  (tdm/god-mode-icon--set-icon)
  (tdm/god-mode-indicator--update-cached))

(defun tdm/god-mode-indicator--update-cached ()
  "A hook function for setting `tdm/god-mode-indicator'."
  (setq tdm/god-mode-indicator--cached
        (format
         "%s" (if (bound-and-true-p god-local-mode)
                  tdm/god-mode-icon
                ""))))

(defun tdm/god-mode-indicator ()
  "Display `td/god-mode-indicator--cached' in the mode-line."
  tdm/god-mode-indicator--cached)

(with-eval-after-load 'god-mode
  (defvar svg-lib-icon-collections) ; Satisfy compiler
  (require 'svg-lib)

  (add-to-list 'svg-lib-icon-collections
               (cons "local" "file:///home/trev/.config/emacs/%s.svg"))
  (tdm/god-mode-setup-icon)
  (add-hook 'god-local-mode-hook #'tdm/god-mode-indicator--update-cached))
```


<a id="org6d56fdc"></a>

### Buffer related info

Numerous functions to help appreciate what's going on with a buffer inclding status flags for read-only, modified or in a client frame.

```elisp
(defun tdm/status-flag (on face)
  "Produce a status flag based on some `PRED'icate test and give it a `FACE'."
  (format "%s" (if on
                   (propertize "▰" 'face `(:inherit ,face :weight bold))
                 "-")))

(defun tdm/line-number-indicator--update ()
  "Display the mode-line buffer position."
  (setq tdm/line-number-indicator
        (if line-number-mode (list "  %l:%c") "")))

(defvar-local tdm/line-number-indicator (tdm/line-number-indicator--update)
  "Display buffer position in the mode-line.")

(add-hook 'line-number-mode-hook #'tdm/line-number-indicator--update)

(defun tdm/buffer-size--update ()
  "Update the `tdm/buffer-size' mode-line variable."
  (setq tdm/buffer-size
        (if size-indication-mode
            '(:propertize " (%I)" :face '(:inherit font-lock-comment-face))
          "")))

(defvar-local tdm/buffer-size (tdm/buffer-size--update)
  "Display the buffer size in the mode-line.")

(add-hook 'size-indication-mode-hook #'tdm/buffer-size--update)
```


<a id="orgff7485d"></a>

### Various other package indicators

Flycheck Flymake, the built in misc info, etc.

```elisp
(defun tdm/flycheck ()
  "Get the flycheck status for the buffer, if LSP mode is not doing so."
  (when (and (bound-and-true-p flycheck-mode)
             (not (bound-and-true-p lsp-mode)))
    (let* ((errlist (flycheck-count-errors flycheck-current-errors))
           (warnings (alist-get 'warning errlist))
           (errors (alist-get 'error errlist)))
      (concat
       (when warnings
         (propertize (format "  %s%s"
                             warnings (if errors "/" ""))
                     'face 'warning))
       (when errors
         (propertize (format
                      "%s%s" (if warnings "" "  ") errors)
                     'face 'error))))))

(defun tdm/flymake ()
  "Display the flymake status for the buffer."
  (when (bound-and-true-p flymake-mode) " "
    flymake-mode-line-title
    flymake-mode-line-exception
    flymake-mode-line-counters))

(defun tdm/misc ()
  "Get a trimmed version of the `mode-line-misc-info'."
  (let ((info (format-mode-line mode-line-misc-info)))
    (unless (string= info "")
      (list "  " (string-trim info)))))

(defun tdm/macro-indicator ()
  "Indicate when a macro is being recorded in the mode-line."
  (when defining-kbd-macro
    (format "%s" (propertize
                  "λ" 'face '(:inherit bold :foreground "#C792EA")))))

(defun tdm/anzu-indicator ()
  (when (bound-and-true-p anzu-mode)
    (concat " " (anzu--update-mode-line))))
```


<a id="orgcb5635f"></a>

### Render a split-view mode-line

This is not a very efficient way to render a modeline but it's the best way I could figure out how to make the columns. On the left side are the stats/flags for the buffer itself. On the right are all of the major/minor mode features

```elisp
(defun tdm/split-format (left right)
  "Format a mode-line with a `LEFT' and `RIGHT' justified list of elements.
The modeline should fit the `window-width' with space between the lists."
  (let ((reserve (length right)))
    (concat left
            " "
            (propertize " "
                        'display
                        `((space :align-to
                                 (- right (- 0 right-margin) ,reserve))))
            right)))

(setq-default mode-line-format
              '((:eval
                 (tdm/split-format
                  ;; Left
                  (format-mode-line
                   '(" "
                     (:eval (tdm/meow-state))
                     (:eval (tdm/evil-state))
                     (:eval (tdm/status-flag buffer-read-only 'error))
                     (:eval (tdm/status-flag (buffer-modified-p) 'warning))
                     (:eval (if (not (eq
                                      (format-mode-line mode-line-client)
                                      ""))
                                (tdm/status-flag t '(:foreground "#C792EA"))
                              ""))
                     " "
                     (:eval (tdm/project))
                     mode-line-buffer-identification
                     tdm/line-number-indicator
                     (:eval (tdm/anzu-indicator))
                     tdm/buffer-size
                     (:propertize " %p%%" face (:inherit font-lock-comment-face))))
                  ;; Right
                  (format-mode-line
                   '((:eval (tdm/god-mode-indicator))
                     (:eval (tdm/macro-indicator))
                     (:eval (tdm/flycheck))
                     tdm/git-status-indicator
                     (:eval (tdm/misc))
                     "  "
                     mode-line-modes))))))
```


<a id="org6a8f038"></a>

### Diminish the minor-mode-alist

The `diminish` package reduces the output from the `minor-mode-alist` in the minibuffer. Due to how lazy-loading works, we want to make sure we have diminish early on.

This greatly reduces noise in the mode-line while being aware of important contextual minor-modes.

```elisp
(defun tdm/diminish-lsp-lighter ()
  "Display the LSP status in the `mode-line-modes'."
  (let* ((lsp-up lsp--buffer-workspaces)
         (color (if lsp-up '(:inherit success :weight bold)
                  '(:inherit warning :weight bold))))
    `(:propertize " LSP" face ,color)))

(dolist (mode '(("company" 'company-mode)
                ("hideshow" 'hs-minor-mode)
                ("undo-tree" 'undo-tree-mode)
                ("whitespace" 'whitespace-mode)
                ("yasnippet" 'yas-minor-mode)
                ("which-key" 'which-key-mode)
                ("org-indent" 'org-indent-mode)
                ("simple" 'visual-line-mode)
                ("eldoc" 'eldoc-mode)
                ("evil-org" 'evil-org-mode)
                ("flycheck" 'flycheck-mode)
                ("flymake" 'flymake-mode)
                ("tree-sitter" 'tree-sitter-mode "TS")
                ("lsp-mode" 'lsp-mode '(:eval (tdm/diminish-lsp-lighter)))
                ("god-mode" 'god-local-mode)
                ("beacon" 'beacon-mode)
                ("evil-goggles" 'evil-goggles-mode)
                ("evil-commentary" 'evil-commentary-mode)
                ("goggles" 'goggles-mode)))
  (eval-after-load (car mode)
    `(diminish ,(cadr mode) ,(caddr mode))))

(diminish 'defining-kbd-macro)

(with-eval-after-load 'meow
  (dolist (mode (list 'meow-normal-mode
                      'meow-insert-mode
                      'meow-motion-mode
                      'meow-keypad-mode
                      'meow-beacon-mode))
    (diminish mode)))
```


<a id="org37a5b52"></a>

## Utility Packages

Configurations for packages that enrich the Emacs experience. Some packages are internal, many are external.


<a id="org7dc9ba9"></a>

### Anzu

```elisp
(with-eval-after-load 'isearch
  (require 'anzu)
  (global-anzu-mode +1)

  (setq anzu-mode-lighter ""
        anzu-deactivate-region t
        anzu-search-threshold 1000
        anzu-replace-threshold 50
        anzu-cons-mode-line-p nil)
  (global-set-key [remap query-replace] #'anzu-query-replace)
  (global-set-key [remap query-replace-regexp] #'anzu-query-replace-regexp))
```


<a id="orgc5465d9"></a>

### Completions

A combination of packages to enhance completions.

1.  Company

    Completions at point/region.
    
    ```elisp
    (defun td/company-prog-hook ()
      "Completions for programming."
      (company-mode)
      (setq-local company-backends
                  '(company-capf :with
                                 company-yasnippet
                                 company-dabbrev-code
                                 company-files)))
    
    (defun td/company-text-hook ()
      "Completions for writing."
      (company-mode))
    
    (add-hook 'prog-mode-hook #'td/company-prog-hook)
    (add-hook 'text-mode-hook #'td/company-text-hook)
    
    (setq company-files-exclusions '(".git/")
          company-idle-delay 0.3)
    ```

2.  Icomplete mode

    ```elisp
    (icomplete-vertical-mode 1)
    (setq icomplete-show-matches-on-no-input t)
    
    (defun td/toggle-flex-completion ()
      "Toggle flex completion."
      (interactive)
      (if (member 'flex completion-styles)
          (progn (message "Flex Off")
                 (setq-local completion-styles (remove 'flex completion-styles)))
        (message "Flex On")
        (setq-local completion-styles `(flex ,@completion-styles))))
    
    (td/bind-keys '(("C-n"        . icomplete-forward-completions)
                    ("C-p"        . icomplete-backward-completions)
                    ("S-<return>" . icomplete-force-complete-and-exit)
                    ("C-c f"      . td/toggle-flex-completion))
                  icomplete-minibuffer-map)
    ```

3.  Orderless

    ```elisp
    (with-eval-after-load 'icomplete
      (require 'orderless)
      (setq completion-styles '(orderless basic)
            completion-category-overrides
            '((file (styles basic partial-completion)))))
    ```

4.  Savehist

    Save history for Vertico to look at later.
    
    ```elisp
    (savehist-mode)
    ```


<a id="orgeaf4657"></a>

### Docker

Docker support is provided by:

-   emacs-docker
-   emacs-dockerfile-mode
-   emacs-docker-compose-mode


<a id="org7600a95"></a>

### Diff-hl

Show me the diffs in the fringe!

```elisp
(setq diff-hl-show-staged-changes nil)
(global-diff-hl-mode)
(with-eval-after-load 'magit
  (add-hook 'magit-pre-refresh-hook 'diff-hl-magit-pre-refresh)
  (add-hook 'magit-post-refresh-hook 'diff-hl-magit-post-refresh))
```


<a id="orgd059f66"></a>

### Ediff

I enjoy using tiling window managers. It serves me better to avoid having a separate, floating window for ediff.

```elisp
(setq ediff-window-setup-function 'ediff-setup-windows-plain)
```


<a id="org25c0822"></a>

### Elfeed

RSS Reader :D

```elisp
(global-set-key (kbd "<f6>") #'elfeed)
```


<a id="orgeed92b9"></a>

### EMMS

Emacs Multi-Media System

```elisp
(defun td/start-emms ()
  "Start up emms."
  (interactive)
  (require 'emms-setup)
  (require 'emms-player-mpd)

  (emms-all)

  (setq emms-player-mpd-server-port "6600"
        emms-player-mpd-music-directory "~/Music"
        emms-player-mpd-server-name "localhost")

  (add-to-list 'emms-player-list 'emms-player-mpd)
  (add-to-list 'emms-info-functions 'emms-info-mpd)

  (emms-mode-line-mode -1)
  (emms-player-mpd-connect)

  (defvar emms-keymap
    (let ((m (make-sparse-keymap)))
      (td/bind-keys '(("e" . emms)
                      ("B" . emms-smart-browse)
                      ("P" . emms-pause)
                      ("n" . emms-next)
                      ("p" . emms-previous)
                      ("f" . emms-seek-forward)
                      ("b" . emms-seek-backward)
                      ("s" . emms-start)
                      ("k" . emms-stop))
                    m)
      m)
    "Keybindings for EMMS.")
  (fset 'emms-keymap emms-keymap)
  (global-set-key (kbd "C-c e") #'emms-keymap)
  (emms-librefm-scrobbler-enable))
```


<a id="org76a51be"></a>

### Surround

My attempt at writing a quick replacement for "vim surround". There are better solutions out there, but when they're unpredictable, I don't want to figure out why.

This is a "dumb" solution. It just seeks backward for the start of a pair, then matches the surround with a forward sexp. If it's called with a neg-arg (eg: `(surround -1)`) it will scan forward first and look back. If the point is not inside the bounds of a resulting backward scan, we fall back to forward.

Ideally if the point is not inside the bounds of a found sexp, I should be scanning recursively in the same direction until it does. Maybe I'll implement this later.

When a pair is not in `surround-pairs`, it will fall-back to symmetrical pairs (a pair of the same char). When this happens, scanning forward, or backward, makes no difference.

It currently doesn't care about the scope of the scan, either, and doesn't care if the backward or forward sexp is 100 lines elsewhere.

I should refine this.

```elisp
(defvar surround-pairs '(("{" . "}")
                         ("(" . ")")
                         ("[" . "]")
                         ("<" . ">"))
  "A list of asymmetric pairs for `surround' to respect.")

(defun surround--seek-outer-boundary (start left pair count)
  "Seek out the boundary of an outside `PAIR' from the `START'.
If `LEFT' is non-nil, seek left. Otherwise, seek right."
  (let* ((search (if left #'search-backward #'search-forward))
         (ch-match (if left (car pair) (cdr pair)))
         (ch-skip  (if left (cdr pair) (car pair)))
         (sym (eq ch-skip ch-match))
         (case-fold-search nil))
    (save-excursion
      (if sym
          (apply search (list ch-match nil t count))
        (let* ((match (apply search (list ch-match nil t count)))
               (mid (push-mark start t t))
               (imbalance (count-matches (regexp-quote ch-skip)
                                         (region-beginning)
                                         (region-end)))
               (mcount (count-matches (regexp-quote ch-match)
                                      (region-beginning)
                                      (region-end))))
          (if (and (>= imbalance mcount) match)
              (surround--seek-outer-boundary start left pair
                                             (+ (- imbalance mcount) 1))
            (deactivate-mark)
            match))))))

(defun surround--seek-bounds (pair)
  "Find the bounds of a surrounding `PAIR' around the point."
  (let ((bounds (cons (surround--seek-outer-boundary (point) t pair 1)
                      (surround--seek-outer-boundary (point) nil pair 1))))
    (if (and (car bounds) (cdr bounds))
        bounds
      (user-error (format "No surrounding pair: %s" pair)))))

(defun surround--add-pair (bounds pair)
  "Add an arbitrary surrounding `PAIR' of chars to a `BOUNDS'."
  (save-excursion
    (goto-char (car bounds))
    (insert (car pair))
    (goto-char (+ (cdr bounds) 1))
    (insert (cdr pair))))

(defun surround--delete-pair (bounds)
  "Delete a surrounding pair outside the `BOUNDS' a range of positions."
  (save-excursion
    (goto-char (- (cdr bounds) 1))
    (delete-char 1)
    (goto-char (car bounds))
    (delete-char 1)))

(defun surround--change-pair (bounds)
  "Swap out an exisiting `PAIR' outside of `BOUNDS'."
  (let* ((to-what (char-to-string (read-char (message "To new pair: "))))
         (new-pair (or (assoc to-what surround-pairs)
                       (rassoc to-what surround-pairs)
                       (cons to-what to-what))))
    (surround--delete-pair bounds)
    (surround--add-pair (cons (car bounds) (- (cdr bounds) )) new-pair)))

(defun surround (neg)
  "Add surrounding pairs to a region, or change/delete an existing pair.
Inspired by vim-surround. Scans forward. Use `NEG'-arg to scan backward
for pair."
  (interactive "p")
  (let* ((reverse (< neg 0))
         (case-fold-search nil)
         (method (if (and (region-active-p)
                          (not (eq (region-beginning) (region-end))))
                     ?a
                   (read-char-choice "(c)hange or (d)elete pair? " '(?c ?d))))
         (target (char-to-string (read-char (message "Pair:"))))
         (pair (or (assoc target surround-pairs)
                   (rassoc target surround-pairs)
                   (cons target target)))
         (bounds (or (and
                      (region-active-p)
                      (car (region-bounds)))
                     (surround--seek-bounds pair))))
    (cond ((eq method ?a) (surround--add-pair bounds pair))
          ((eq method ?c) (surround--change-pair bounds))
          ((eq method ?d) (surround--delete-pair bounds)))))

(global-set-key (kbd "C-S-s") #'surround)
```


<a id="org23ab139"></a>

### Ement

A Matrix client for Emacs.

```elisp
(defun td/matrix-connect ()
  "Connect to Matrix via Ement & Pantalaimon."
  (interactive)
  (ement-connect
   :user-id "@trevdev:matrix.org"
   :password (password-store-get "Personal/matrix.org")
   :uri-prefix "http://localhost:8009"))
```


<a id="org0f0f439"></a>

### ERC

```elisp
(setq erc-autojoin-channels-alist
      '(("Libera.Chat" "#emacs" "#systemcrafters" "#stumpwm")))

(defun td/launch-erc ()
  (interactive)
  (erc-tls :server "irc.libera.chat"
                             :port 7000
                             :nick "trevdev"
                             :password (password-store-get
                                        "Biz/libera.chat")))
```


<a id="org4eda760"></a>

### Eshell

```elisp
(defun td/eshell-extras ()
  "Start extra features for eshell-mode"
  (eshell-syntax-highlighting-mode))

(add-hook 'eshell-mode-hook #'td/eshell-extras)
```


<a id="orgc04d505"></a>

### Evil

```elisp
evil
```

1.  Keybinds

    Evil requires a lot of key re-binding in order to get going. You may still find yourself using `M-x` from time to time, looking for some keybind and discovering it's something like `C-c C-x M-o q r s` and think "yeah, that's easy!" Just kidding. You'll want to create mode-specific (or global) leader, normal or motion mapping.
    
    ```elisp
    (defun td/evil-bind-keys ()
      "Create some extra evil bindings."
      (evil-set-leader 'normal (kbd "SPC"))
      ;; Avy
      (evil-define-key 'normal 'global (kbd "<leader>;") 'avy-goto-char-timer)
      ;; General
      (evil-define-key 'normal 'global (kbd "<leader>ff") 'find-file)
      (evil-define-key 'normal 'global (kbd "<leader>bb") 'switch-to-buffer)
      (evil-define-key 'normal 'global (kbd "<leader>bn") 'next-buffer)
      (evil-define-key 'normal 'global (kbd "<leader>bp") 'previous-buffer)
      (evil-define-key 'normal 'global (kbd "<leader>x") 'execute-extended-command)
      ;; LSP
      (evil-define-key 'normal lsp-mode-map (kbd "K") 'lsp-ui-doc-glance)
      ;; Org
      (evil-define-key 'normal org-mode-map (kbd "<leader>ci") 'org-clock-in)
      (evil-define-key 'normal org-mode-map (kbd "<leader>co") 'org-clock-out)
      (evil-define-key 'normal org-mode-map (kbd "<leader>'") 'org-edit-special)
      (evil-define-key 'normal org-src-mode-map (kbd "<leader>'") 'org-edit-special)
      (evil-define-key 'motion org-agenda-mode-map (kbd "sf") 'org-agenda-filter)
    
      (evil-define-key 'normal 'global (kbd "<leader>a") 'org-agenda)
      (evil-define-key 'normal 'global (kbd "<leader>i") 'td/eldoc-box-help)
      (evil-define-key 'normal 'global (kbd "<leader>cg") 'org-clock-goto)
      ;; Magit
      (evil-define-key 'normal 'global (kbd "gs") 'magit))
    ```

2.  Extending Evil Mode

    There are a lot of packages that make Evil better by extending it. Thankfully they are easy to set up.
    
    The following sub-headlines will be tangled into this block:
    
    ```elisp
    (with-eval-after-load 'evil
      (evil-lion-mode)
      (global-evil-matchit-mode 1)
      (global-evil-surround-mode 1)
      (evil-snipe-mode 1)
      (evil-snipe-override-mode t)
      (add-hook 'magit-mode-hook #'turn-off-evil-snipe-override-mode)
      (evil-exchange-install)
      (setq evil-goggles-pulse t)
      (defun evil-goggles-start-later ()
        "Load goggles later so that it works better in a client frame."
        (evil-goggles-mode))
      
      (add-hook 'evil-insert-state-entry-hook #'evil-goggles-start-later)
      (add-hook 'evil-insert-state-exit-hook
                #'(lambda ()
                    (remove-hook
                     'evil-insert-state-entry-hook 'evil-goggles-start-later)))
      (require 'evil-multiedit)
      (evil-multiedit-default-keybinds)
      (add-hook 'prog-mode-hook #'evil-commentary-mode)
      (evil-collection-init)
      (with-eval-after-load 'org
        (require 'evil-org)
        (require 'evil-org-agenda)
        (evil-org-agenda-set-keys)
        (add-hook 'org-mode-hook #'evil-org-mode)))
    ```
    
    1.  evil-lion
    
        Evil-lion is for making emac's built-in `align` function more "evil" friendly the motion `gl` (align right) or `gL` (align left). For example, `glp.` would left align all elements in a paragraph on a period character.
    
    2.  evil-matchit
    
        The essential pair matching plugin for vim ported to evil-mode.
    
    3.  evil-surround
    
        The essential pair swapping plugin by the venerable Tim Pope ported to evil-mode.
    
    4.  evil-snipe
    
    5.  evil-exchange
    
        Be able to swap two motion-selected areas with the `gx` motion.
    
    6.  evil-goggles
    
    7.  evil-multiedit
    
        Multiedit is sorta like the venerable multiple cursors plugin, only it's vimish and frankly, not as good. It's still better than writing macros for everything.
        
        Unfortunately, `evil-multiedit-default-keybinds` overrides common meta functions like delete-word (M-d). Theoretically, we would not be using these with evil anyway.
    
    8.  evil-commentary
    
        Make commenting code motion-friendly with the `gc` motion.
    
    9.  evil-collection
    
        This package is <span class="underline">massive</span>. It is a collaborative, community effort to bring sane evil keybinds to as many major modes as possible. Its goal is to keep things consistent and as predictable as possible.
        
        It does add a lot of package bloat, however. Without it, many major modes dump you into "Emacs mode". If you're used to, and are okay with the occasional Emacs only interface, you might wanna skip this one.i
    
    10. evil-org
    
        Evil-org *greatly* improves the org-mode experience in evil-mode Emacs.

3.  Apply Evil Configurations

    Here apply our evil configurations and set up our hooks.
    
    ```elisp
    (defun evil/start ()
      "Start evil-mode."
      (setq evil-want-C-u-scroll t)
      (setq evil-want-keybind nil)
      (require 'evil)
      (setq evil-visual-state-cursor 'hbar
            evil-insert-state-cursor '(bar . 4))
      (customize-save-variable 'evil-undo-system 'undo-redo)
      (td/evil-bind-keys)
      (evil-mode 1))
    
    (add-hook 'after-init-hook #'evil/start)
    ```


<a id="orgd97d941"></a>

### Expand Region

It just makes selecting text between sexps easy.

```elisp
(defun td/setup-expand-region ()
  (require 'expand-region)
  (td/bind-keys '(("C-=" . er/expand-region)))
  (defvar er/keymap
    (let ((map (make-sparse-keymap "er/objects")))
      (td/bind-keys '(("w"   . er/mark-word)
                      ("W"   . er/mark-symbol)
                      ("s"   . er/mark-sentence)
                      ("p"   . er/mark-paragraph)
                      ("e"   . er/mark-email)
                      ("d"   . er/mark-defun)
                      ("u"   . er/mark-url)
                      ("o p" . er/mark-outside-pairs)
                      ("i p" . er/mark-inside-pairs)
                      ("o s" . er/mark-outside-quotes)
                      ("i s" . er/mark-inside-quotes)
                      ("o e" . er/mark-org-element)
                      ("o E" . er/mark-org-element-parent))
                    map)
      map)
    "A keymap for quickly calling expand region functions.
\\{er/keymap}")
  (fset 'er/keymap er/keymap)
  (define-key priority-mode-map (kbd "C-,") er/keymap))

(add-hook 'after-init-hook #'td/setup-expand-region)
```


<a id="org8d1baf7"></a>

### Imenu

```elisp
(global-set-key (kbd "C-c i") #'imenu)
```


<a id="org46abf74"></a>

### Magit

Magit is one of the biggest reasons why I fell in love with emacs. It's the best keyboard driven "TUI" abstraction of the git command line anywere, period. Better than Fugitive by far. Sorry, Tim Pope.

```elisp
(global-set-key (kbd "C-c m") #'magit-status)
```


<a id="orgafbf924"></a>

### Org

The greatest part of using Emacs is org-mode. It handles my agenda, my todo list, helps me prioritize tasks, track time and invoice clients.

1.  Key Variables

    I am using tags to help sort contexts within my agenda. Some people use categories for that. I technically do that, too, but I also use separate files. Filenames are categories by default, so there is less to configure when you use separate files.
    
    ```elisp
    (defvar td/tag-list
      '((:startgroup)
        ("@home" . ?H)
        ("@work" . ?W)
        (:endgroup)
        ("foss"  . ?f)
        ("gurps" . ?g)
        ("idea"  . ?i))
      "The tags for org headlines.")
    ```
    
    Next are my TODO key words. They are meant to be used as such:
    
    -   `TODO` A generic task or actionable thing.
    -   `NEXT` A planned task, something I am setting my mind to until it is done. There should be very few of these types of tasks so that I am setting achievable goals
    -   `WAIT` The task that is held up by some pre-requesite or external factor
    -   `LOW` The task is a "maybe/someday" task. I'd like to see it done, but it's not a priority right now.
    -   `DONE` The task is completed
    -   `PASS` The task has been "passed along" or "delegated" to someone else. Considered 'done', just not by myself
    -   `CANC` The task has been cancelled or ended before completion
    
    ```elisp
    (defvar td/todo-keywords
      '((sequence "TODO(t)" "NEXT(n)" "WAIT(w@/!)" "LOW(l)"
                  "|" "DONE(d!)" "PASS(p@)" "CANC(k@)"))
      "A sequence of keywords for Org headlines.")
    ```
    
    My org agenda commands & stuck projects. Currently a work in progress! I am reading David Allen's "[Getting Things Done](https://gettingthingsdone.com/)." I am attempting to shape my agenda to suit that system.
    
    ```elisp
    (defvar td/org-agenda-commands
      '(("d" "Dashboard: Get things done!"
         ((agenda "" ((org-agenda-span 7)))
          (tags-todo "+refile"
                     ((org-agenda-overriding-header "Unfiled")))
          (tags-todo "+PRIORITY=\"A\""
                     ((org-agenda-overriding-header "High Priority")
                      (org-agenda-skip-function
                       '(org-agenda-skip-entry-if 'todo '("WAIT")))))
          (todo "NEXT"
                ((org-agenda-overriding-header "Do Next")
                 (org-agenda-max-todos nil)))
          (todo "WAIT"
                ((org-agenda-overriding-header "Follow Up")))
          (todo "TODO"
                ((org-agenda-overriding-header "Other Actionables")
                 (org-agenda-skip-function
                  '(org-agenda-skip-entry-if 'scheduled 'deadline))))
          )
         )
        ("l" "Backburner of low priority tasks"
         ((todo "LOW"
               ((org-agenda-overriding-header "Someday/Maybe"))))
         )
        )
      "Custom commands for Org Agenda.")
    ```
    
    Capture templates! These help me collect information into Org files. Currently I only have 2 cookbook capture methods that are meant to be used with org-chef. See [extensions](#orga975fac) for how I extend org-mode.
    
    ```elisp
    (defvar td/capture-templates
      '(("t" "Todo" entry (file "~/Org/agenda/inbox.org")
         "* TODO %^{Title: }\n:PROPERTIES:\n:date: %U\n:END:\n%?"
         :empty-lines 1)
        ("c" "Contact" entry (file+headline "~/Org/contacts.org" "Other")
         "* %^{Name: }\n:PROPERTIES:\n:email: %?\n:END:"
         :empty-lines 1))
      "Base org-capture-templates.")
    
    (global-set-key (kbd "C-c M-a") #'org-capture)
    ```
    
    I usually stick to monospace sized fonts with the exception of Org files. I like the first 3 levels to be slightly larger than the rest, and progressively smaller. This helps me create a sense of urgency at the lower-level headers and it also improves readability.

2.  Functions

    Some fairly self-explanatory utility functions.
    
    ```elisp
    (defvar td/org-scale-levels-enable nil
      "Whether or levels are scaled.")
    
    (defun td/org-scale-levels-toggle (&optional enable)
      "Enlarge org levels for more readability."
      (interactive)
      (let ((scaled (or enable (not td/org-scale-levels-enable))))
        (dolist (face `((org-level-1 . (if ,scaled 1.2 1.0))
                        (org-level-2 . (if ,scaled 1.1 1.0))
                        (org-level-3 . (if ,scaled 1.05 1.0))))
          (set-face-attribute (car face) nil :weight 'semi-bold :height (eval (cdr face))))
        (setq td/org-scale-levels-enable scaled)))
    
    (defun td/org-hook ()
      "Do some stuff on org mode startup."
      (org-clock-persistence-insinuate)
      (org-indent-mode)
      (setq-local line-spacing 0.1))
    
    (defun td/org-append-templates (templates)
      (setq org-capture-templates (append org-capture-templates templates)))
    
    ;; Fix completion in steps for org-refile
    (advice-add #'org-olpath-completing-read :around
                (lambda (&rest args)
                  (minibuffer-with-setup-hook
                      (lambda () (setq-local completion-styles '(basic)))
                    (apply args))))
    ```

3.  Apply Configuration

    ```elisp
    (add-hook 'org-mode-hook #'td/org-hook)
    (global-set-key (kbd "C-c a") #'org-agenda)
    
    (with-eval-after-load 'org
      (define-key org-mode-map (kbd "C-c e t") #'org-table-export))
    
    (with-eval-after-load 'ox
      (require 'ox-md nil t))
    
    (setq org-fontify-quote-and-verse-blocks t
          org-attach-auto-tag "attach"
          org-directory "~/Org"
          org-archive-location "archives/%s_archive::"
          org-log-done 'time
          org-log-into-drawer t
          org-enforce-todo-dependencies t
          org-src-preserve-indentation t
          org-clock-persist 'history
          org-agenda-block-separator "──────────"
          org-agenda-tags-column -80
          org-duration-format '(("h" . nil) (special . 2))
          org-clock-total-time-cell-format "%s"
          org-agenda-files '("~/Org/agenda")
          org-tag-alist td/tag-list
          org-todo-keywords td/todo-keywords
          org-refile-use-outline-path 'file
          org-refile-allow-creating-parent-nodes t
          org-outline-path-complete-in-steps t
          org-refile-targets '((org-agenda-files :maxlevel . 4)
                               ("contacts.org" :maxlevel . 1))
          org-clock-sound "~/.config/emacs/inspectorj_bell.wav"
          org-timer-default-timer "25"
          org-agenda-custom-commands td/org-agenda-commands
          org-stuck-projects '("/PROJ-DONE" ("TODO" "NEXT") nil "- \\[ \\]")
          org-capture-templates td/capture-templates
          org-catch-invisible-edits 'show-and-error
          org-special-ctrl-a/e t
          org-insert-heading-respect-content t)
    
    (add-to-list 'display-buffer-alist '("\\*Org Agenda*\\*"
                                         (display-buffer-in-direction)
                                         (direction . right)
                                         (window-width . 0.50)
                                         (window-height . fit-window-to-buffer)))
    ```

4.  Extending Org Mode

    Extending org-mode with some interesting packages.
    
    1.  org-chef
    
        [Org-chef](https://github.com/Chobbes/org-chef) is a must have if you enjoy cooking. You can just use `M-x org-chef-insert-recipe` in whatever cookbook file, or the capture templates.
        
        ```elisp
        (td/org-append-templates
         '(("r" "Recipe" entry (file "~/Projects/cookbook/src/cookbook.org")
            "%(org-chef-get-recipe-from-url)"
            :empty-lines 1)
           ("m" "Manual Cookbook" entry
            (file "~/Projects/cookbook/src/cookbook.org")
            (eval (concat "* %^{Recipe title: }\n  :PROPERTIES:\n  :source-url:\n"
                    "  :servings:\n  :prep-time:\n  :cook-time:\n  :ready-in:\n"
                    "  :END:\n** Ingredients\n   %?\n** Directions\n\n")))))
        ```
    
    2.  ox-gfm
    
        Get access to Github Flavored Markdown
        
        ```elisp
        (with-eval-after-load 'ox
          (require 'ox-gfm))
        ```
    
    3.  ox-hugo
    
        I like org-publish, but there are some files (like my cookbook) that I would like to keep in one document, as it is a capture file, and be able to easily publish it into a list of "posts".
        
        ```elisp
        (with-eval-after-load 'ox
          (require 'ox-hugo))
        ```
    
    4.  org-present
    
        A tiny package for presenting with org-mode.
        
        ```elisp
        (setq org-present-text-scale 5)
        (with-eval-after-load 'org-present
          (add-hook 'org-present-mode-hook
                    #'(lambda ()
                        (org-present-big)
                        (td/org-scale-levels-toggle t)
                        (org-display-inline-images)
                        (blink-cursor-mode -1)
                        (org-present-hide-cursor)
                        (org-present-read-only)))
          (add-hook 'org-present-mode-quit-hook
                    #'(lambda()
                        (org-present-small)
                        (org-remove-inline-images)
                        (org-present-show-cursor)
                        (blink-cursor-mode 1)
                        (td/org-scale-levels-toggle)
                        (org-present-read-write)))
          (td/bind-keys '(("C-c C-p C-c" . org-present-show-cursor)
                          ("C-c C-p C-h" . org-present-hide-cursor))
                        org-present-mode-keymap))
        ```
    
    5.  org-roam
    
        Org roam is an incredible thought capture system, inspired by roam research. I'm not sure this one's for me, but I am giving it a try.
        
        ```elisp
        (defvar td/roam-capture-templates
          '(("d" "default" plain "%?"
             :target (file+head "%<%Y%m%d%H%M%S>-${slug}.org"
                                "#+TITLE: ${title}\n#+DATE: %U\n")
             :unnarrowed t)))
        
        (defvar td/roam-capture-daily
          '(("d" "default" entry "* %<%I:%M %p>: %?"
             :target (file+head "%<%Y-%m-%d>.org"
                                "#+TITLE: %<%a, %b %d %Y>\n"))
            ("p" "Private" entry "* %<%I:%M %p>: %?"
             :target (file+head "%<%Y-%m-%d>.org.gpg"
                                "#+TITLE: %<%a, %b %d %Y>\n"))))
        
        (defvar td/roam-display-template
          (concat "${title} "
                  (propertize "${tags}" 'face 'org-tag)))
        
        (td/bind-keys '(("C-c r t" . org-roam-buffer-toggle)
                        ("C-c r f" . org-roam-node-find)
                        ("C-c r i" . org-roam-node-insert)
                        ("C-c r c" . org-roam-capture)
                        ("C-c r d i" . org-roam-dailies-capture-today)
                        ("C-c r d t" . org-roam-dailies-goto-today)
                        ("C-c r d y" . org-roam-dailies-goto-yesterday)
                        ("C-c r d d" . org-roam-dailies-goto-date)))
        
        (setq org-roam-capture-templates td/roam-capture-templates
              org-roam-dailies-capture-templates td/roam-capture-daily
              org-roam-node-display-template td/roam-display-template
              org-roam-db-node-include-function
              (lambda ()
                (not (member "attach" (org-get-tags))))
              org-roam-directory (file-truename "~/Org/roam"))
        
        (add-to-list 'display-buffer-alist '("\\*org-roam\\*"
                                             (display-buffer-in-direction)
                                             (direction . right)
                                             (window-width . 0.33)
                                             (window-height . fit-window-to-buffer)))
        
        
        
        (with-eval-after-load 'org-roam
          (org-roam-db-autosync-mode))
        ```
    
    6.  org-roam-ui
    
        A fancy, web-based user interface for reviewing your org-roam notes and how they connect to one-another.
        
        ```elisp
        (setq org-roam-ui-sync-theme t
              org-roam-ui-follow t
              org-roam-ui-update-on-save t
              org-roam-ui-open-on-start t)
        ```
    
    7.  org-invoice-table
    
        A custom table formatter for invoicing.
        
        ```elisp
        (load-file "~/Projects/org-invoice-table/org-invoice-table.el")
        ```


<a id="org69e45d4"></a>

### Ledger

Knowing what resources you have at your disposal and learning how to budget are powerful things.

```elisp
(setq ledger-use-native-highlighting t)
```


<a id="orgf777ad5"></a>

### Notmuch

Notmuch is a really impressive way to read and organize mail via tagging files. It works really quickly and the configuration is really flexible.

1.  Built In Mail Settings

    ```elisp
    (setq send-mail-function 'sendmail-send-it
          sendmail-program "~/.nix-profile/bin/msmtp"
          message-directory "~/.local/share/mail"
          mail-specify-envelope-from t
          mail-envelope-from 'header
          message-sendmail-envelope-from 'header
          message-signature-directory "~/.local/share/mail/signatures"
          message-signature-file "default")
    ```

2.  Notmuch

    ```elisp
    (setq notmuch-fcc-dirs
          '(("trev@fastmail.com" . "fastmail/Sent")
            ("trev@trevdev.ca"   . "fastmail/Sent")
            ("tn@eml.cc"         . "fastmail/Sent")
            ("trevor@voltagenewmedia.com" . "voltage/Sent"))
          notmuch-saved-searches '(
                                   (:name "todo"
                                          :query "tag:todo"
                                          :key "t"
                                          :sort-order newest-first)
                                   (:name "flagged"
                                          :query "tag:flagged"
                                          :key "f"
                                          :sort-order newest-first)
                                   (:name "personal"
                                          :query "not tag:work"
                                          :count-query "not tag:work and tag:unread"
                                          :key "p"
                                          :sort-order newest-first)
                                   (:name "work"
                                          :query "tag:work"
                                          :count-query "tag:work and tag:unread"
                                          :key "w"
                                          :sort-order newest-first)
                                   (:name "lists"
                                          :query "tag:list"
                                          :count-query "tag:list and tag:unread"
                                          :key "l"
                                          :sort-order newest-first)
                                   (:name "drafts"
                                          :query "tag:draft"
                                          :key "d"
                                          :sort-order newest-first)
                                   (:name "sent"
                                          :query "tag:sent"
                                          :count-query "tag:nil"
                                          :key "s"
                                          :sort-order newest-first)
                                   (:name "archive"
                                          :count-query "tag:nil"
                                          :query "tag:archive"
                                          :key "a"
                                          :sort-order newest-first)
                                   (:name "all mail"
                                          :query "*"
                                          :count-query "tag:nil"
                                          :key "A"
                                          :sort-order newest-first))
          notmuch-archive-tags '("+archive" "-inbox")
          notmuch-tagging-keys '(("a" notmuch-archive-tags "Archive")
                                 ("u" notmuch-show-mark-read-tags "Mark read")
                                 ("f" ("+flagged") "Flag")
                                 ("s" ("+spam" "-inbox") "Mark as spam")
                                 ("d" ("+deleted" "-inbox") "Delete"))
          notmuch-show-logo nil
          notmuch-mua-user-agent-function 'notmuch-mua-user-agent-full
          notmuch-hello-thousands-separator ","
          mml-secure-openpgp-encrypt-to-self t)
    
    (global-set-key (kbd "<f5>") #'notmuch)
    
    (defun td/specify-msmtp-account ()
      (save-excursion
        (beginning-of-buffer)
        (search-forward "From:")
        (setq message-sendmail-extra-arguments
              (if (string-match-p (regexp-quote "voltagenewmedia")
                                  (thing-at-point 'line t))
                  (list "-a" "voltage")
                (list "-a" "default")))))
    
    (add-hook 'notmuch-mua-send-hook #'td/specify-msmtp-account)
    
    (autoload 'notmuch-message-mode "notmuch-mua.el"
      "Activate the notmuch message mode and its corresponding keymap" t)
    ```

3.  ol-notmuch

    The **emacs-ol-notmuch** package gives me the ability to link notmuch message files in org-mode.

4.  org-mime

    Edit messages in Orgdown and transform them into multipart html messages with the **org-mime** package.
    
    ```elisp
    (autoload 'org-mime-edit-mail-in-org-mode "org-mime"
      "Edit a message in org-mode"
      t)
    
    (setq org-mime-export-options
          '(:with-latex dvipng :section-numbers nil :with-author nil :with-toc nil))
    
    (with-eval-after-load 'notmuch
      (td/bind-keys '(("C-c C-o" . org-mime-edit-mail-in-org-mode)
                      ("C-c C-h" . org-mime-htmlize))
                    message-mode-map))
    ```

5.  org-contacts

    Organize contacts with org-mode.
    
    ```elisp
    (with-eval-after-load 'org
      (require 'org-contacts)
      (setq org-contacts-files '("~/Org/contacts.org")))
    ```


<a id="orgc64ec82"></a>

### Password Store

```elisp
(td/bind-keys '(("C-c p c" . password-store-copy)
                ("C-c p f" . password-store-copy-field)
                ("C-c p i" . password-store-insert)
                ("C-c p g" . password-store-generate)))
```


<a id="org1c19d34"></a>

### Sensitive Mode

Inspired from a script written by [Anirudh Sasikumar](https://anirudhsasikumar.net/blog/2005.01.21.html). It has been adapted to accomodate undo-tree. This prevents emacs from generating unencrypted backups & autosave data from `.gpg` files.

```elisp
(define-minor-mode sensitive-mode
  "A minor-mode for preventing auto-saves and back-ups for encrypted files."
  :global nil
  :lighter " Sensitive"
  :init-value nil
  (if (symbol-value sensitive-mode)
      (progn
        ;; disable backups
        (set (make-local-variable 'backup-inhibited) t)
        ;; disable auto-save
        (if auto-save-default
            (auto-save-mode -1))
        ;; disable undo-tree history(?)
        (when (bound-and-true-p undo-tree-mode)
          (undo-tree-mode -1)))
    (kill-local-variable 'backup-inhibited)
    (if auto-save-default
        (auto-save-mode 1))
    (when (bound-and-true-p global-undo-tree-mode)
      (undo-tree-mode 1))))
```


<a id="orgcf20fa4"></a>

### RG

```elisp
(rg-enable-default-bindings)
```


<a id="orgf3e1834"></a>

### Tree-Sitter

```elisp
(defun tree-sitter-start ()
  "Start up tree-sitter."
  (interactive)
  (global-tree-sitter-mode)
  (add-hook 'tree-sitter-after-on-hook #'tree-sitter-hl-mode))
```


<a id="org83af6eb"></a>

### Visual Fill Column

Creates a fake "fill column" to wrap text around. Makes reading documents more visually appealing without breaking text into newlines.

```elisp
(defun td/visual-fill-setup ()
  "Center the column 100 characters wide."
  (setq-local visual-fill-column-width 100
              visual-fill-column-center-text nil)
  (visual-fill-column-mode 1))

(with-eval-after-load 'org
  (define-key org-mode-map (kbd "C-c M-v") #'visual-fill-column-mode))

(add-hook 'org-mode-hook #'td/visual-fill-setup)
```


<a id="org5cd7df9"></a>

### Which-key

What the heck was that keybind again? If you can remember how it starts, which-key can help you find the rest.

```elisp
(which-key-mode)
```


<a id="org99aa060"></a>

### Windmove

Set up a keymap for wind-move and bind it to a prefix that's easy to hit.

```elisp
(defvar td/windmove-map
  (let ((map (make-sparse-keymap)))
    (td/bind-keys '(("e"   . windmove-right)
                    ("a"   . windmove-left)
                    ("n"   . windmove-down)
                    ("p"   . windmove-up)
                    ("s e" . windmove-swap-states-right)
                    ("s a" . windmove-swap-states-left)
                    ("s n" . windmove-swap-states-down)
                    ("s p" . windmove-swap-states-up)
                    ("d e" . windmove-delete-right)
                    ("d a" . windmove-delete-left)
                    ("d n" . windmove-delete-down)
                    ("d p" . windmove-delete-up)
                    ("d d" . delete-window)
                    ("D" . delete-other-windows)
                    ("o"   . other-window)
                    ("v"   . split-window-right)
                    ("h"   . split-window-below)
                    ("="   . enlarge-window)
                    ("-"   . shrink-window)
                    ("b"   . balance-windows))
                  map)
    map)
  "A keymap for windmove functions.
\\{td/windmove-map}")

(fset 'td/windmove-map td/windmove-map)

(global-set-key (kbd "M-o") td/windmove-map)
```


<a id="orgb7ba7a9"></a>

## Syntax Support

This section is all about enhancing support for programming languages. Some of the headlines are package configurations. Others are a record of enhanced syntax support, most likely through some third party package that requires no further configuration.


<a id="orgaa22957"></a>

### Clojure

Clojure is supported by `clojure-mode` and `cider`.

```elisp
(td/auto-mode '(("\\.clj\\'" . clojure-mode)))
```


<a id="orgfd0363d"></a>

### Lisp

This configuration has enhanced support for the following dialects:

-   Common Lisp with the `sly` REPL package
-   Guile Scheme with the `geiser-guile` REPL package


<a id="orga679efd"></a>

### CSS/SCSS

```elisp
(setq css-indent-offset 2
      tab-width 2)
```


<a id="org570ebcb"></a>

### Emmet

Emmet is its own snippet engine for expanding html tags and nesting and is made possible by `emmet-mode`.

```elisp
(setq emmet-expand-jsx-className t)
(td/add-hooks '(sgml-mode
                css-mode
                web-mode
                svelte-mode)
              #'emmet-mode)
```


<a id="org00fb25e"></a>

### Flycheck

The popular `flycheck` package helps me lint various files. I use this in conjunction with [lsp-mode](#org40c7bbe) as the two are meant to work together.

```elisp
(td/add-hooks '(emacs-lisp-mode prog-mode ledger-mode) #'flycheck-mode)
(define-key prog-mode-map (kbd "C-c f") #'flycheck-mode)
(with-eval-after-load 'flycheck
  (setq flycheck-checker-error-threshold 1000))
```


<a id="org40c7bbe"></a>

### LSP Mode

LSP support is delivered by `lsp-mode` and `lsp-ui`.

```elisp
(td/add-hooks '(css-mode
                scss-mode
                html-mode
                js-mode
                json-mode
                python-mode
                php-mode
                ruby-mode
                rust-mode
                scss-mode
                svelte-mode
                typescript-mode
                vue-mode
                yaml-mode)
              #'lsp)
(add-hook 'lsp-mode-hook #'lsp-enable-which-key-integration)

(setq lsp-keymap-prefix "C-c l")
(setq lsp-log-io nil
      lsp-modeline-code-actions-segments '(count)
      lsp-signature-doc-lines 1
      lsp-enable-folding nil
      lsp-completon-provider :none
      lsp-clients-typescript-server-args '("--stdio"
                                           "--tsserver-log-file"
                                           "/dev/stderr")
      lsp-keep-workspace-alive nil)

(with-eval-after-load 'lsp-mode
  (define-key lsp-mode-map (kbd "C-S-H") #'lsp-ui-doc-glance)
  (lsp-register-client
   (make-lsp-client :new-connection (lsp-stdio-connection
                                     "theme-check-language-server")
                    :activation-fn (lsp-activate-on "shopify")
                    :server-id 'theme-check))
  (add-to-list
   'lsp-file-watch-ignored-directories "[/\\]env' [/\\]__pycache__'")
  (add-to-list 'lsp-language-id-configuration
               '(shopify-mode . "shopify")))
```


<a id="org2b19dc8"></a>

### Lispy

```elisp
(td/add-hooks '(lisp-mode
                scheme-mode
                clojure-mode
                emacs-lisp-mode)
              #'lispy-mode)
```


<a id="org9bea7ad"></a>

### Markdown

The free software documentation language of the Internet. Markdown support is made better with `markdown-mode`.

```elisp
(td/auto-mode '(("README\\.md\\'" . gfm-mode)
                ("\\.md\\'" . markdown-mode)
                ("\\.markdown\\'" . markdown-mode)))
```


<a id="orgfd2d7fc"></a>

### Nix Mode

Syntax highlighting for Nix configuration files.


<a id="org40a8652"></a>

### PHP

PHP support is made better with the `php-mode` package.

```elisp
(defun td/get-intelephense-key ()
  "Get my intelephense license key."
  (with-temp-buffer
    (insert-file-contents "~/Documents/intelephense.txt")
    (buffer-string)))

(defun td/get-wordpress-stubs ()
  "The stubs required for a WordPress Project"
  (json-insert ["apache" "bcmath" "bz2" "calendar" "com_dotnet" "Core"
                "ctype" "curl" "date" "dba" "dom" "enchant" "exif"
                "fileinfo" "filter" "fpm" "ftp" "gd" "hash" "iconv" "imap"
                "interbase" "intl" "json" "ldap" "libxml" "mbstring"
                "mcrypt" "meta" "mssql" "mysqli" "oci8" "odbc" "openssl"
                "pcntl" "pcre" "PDO" "pdo_ibm" "pdo_mysql" "pdo_pgsql"
                "pdo_sqlite" "pgsql" "Phar" "posix" "pspell" "readline"
                "recode" "Reflection" "regex" "session" "shmop" "SimpleXML"
                "snmp" "soap" "sockets" "sodium" "SPL" "sqlite3" "standard"
                "superglobals" "sybase" "sysvmsg" "sysvsem" "sysvshm" "tidy"
                "tokenizer" "wddx" "xml" "xmlreader" "xmlrpc" "xmlwriter"
                "Zend OPcache" "zip" "zlib" "wordpress"]))
```


<a id="orgff5a920"></a>

### Prettier

An opinionated way to clean up my web-dev code quickly. Prettier is made possible with the `prettier` package.


<a id="org8de346d"></a>

### Python

Python support is enhanced with the `pyvenv` package. It helps manage virtualenvs.


<a id="org3cd2e08"></a>

### Rainbow Delimiters

The `rainbow-delimeters` package makes nested scopes much easier to follow.

```elisp
(add-hook 'prog-mode-hook #'rainbow-delimiters-mode)
```


<a id="org7496449"></a>

### Rainbow Mode

The `rainbow-mode` package makes color names and hex codes pop with their actual colors.


<a id="org060231a"></a>

### Ruby


<a id="orge8907ab"></a>

### Rust

Enhanced Rust support comes from the `rust-mode` package.

```elisp
(defun td/rust-run-args (s)
  (interactive "sOptional Args:")
  (rust--compile (concat "%s run " s) rust-cargo-bin))

(with-eval-after-load 'rust-mode
  (td/bind-keys '(("C-c c r" . rust-run)
                  ("C-c c a r" . td/rust-run-args))
                rust-mode-map))
```


<a id="orgfa1e89f"></a>

### Shopify Liquid

Enhance support for Shopify's own templating language. LSP support requires the use of Shopify's theme-check language server. All of this brings me full Shopify developer support in Emacs.

```elisp
(define-derived-mode shopify-mode web-mode "Shopify"
  "Use web mode to highlight shopify liquid files")
(provide 'shopify-mode)
(add-to-list 'auto-mode-alist '("\\.liquid\\'" . shopify-mode))
(defvar liquid-electric-pairs '((?% . ?%))
  "Electric pairs for liquid syntax.")
(defun liquid-add-electric-pairs ()
  (setq-local electric-pair-pairs (append electric-pair-pairs
                                          liquid-electric-pairs)
              electric-pair-text-pairs electric-pair-pairs))
(add-hook 'shopify-mode-hook #'liquid-add-electric-pairs)
```


<a id="org8f70b95"></a>

### Svelte

I extend [web-mode](#org8df1527) to enhance the syntax highlighting for `.svelte` files.

```elisp
(define-derived-mode svelte-mode web-mode "Svelte"
  "I just want web-mode highlighting with .svelte files")
(provide 'svelte-mode)
(add-to-list 'auto-mode-alist '("\\.svelte\\'" . svelte-mode))
```


<a id="orgc6805d1"></a>

### TypeScript & JavaScript

I prefer the more minimal `typescript-mode` package for enhanced typescript syntax. The of my JS is handled by the built in `js-mode` and the typescript-language-server.

```elisp
(add-to-list 'auto-mode-alist '("\\.tsx\\'" . typescript-mode))

(setq js-indent-level 2)
(setq typescript-indent-level 2)
```


<a id="orgf046ae3"></a>

### VueJS

I extend [web-mode](#org8df1527) to enhance highlighting in `.vue` files.

```elisp
(define-derived-mode vue-mode web-mode "VueJS"
  "I just want web-mode highlighting with .svelte files")
(provide 'vue-mode)
(add-to-list 'auto-mode-alist '("\\.vue\\'" . vue-mode))
```


<a id="org8df1527"></a>

### Web Mode

There isn't a much better catch-all for web template syntax support than web-mode.

I don't like all of it's default tab-width settings so I override those.

```elisp
(td/auto-mode '(("\\.html\\'" . web-mode)))
(setq web-mode-markup-indent-offset tab-width
      web-mode-code-markup-indent-offset tab-width
      web-mode-style-padding tab-width
      web-mode-script-padding tab-width
      web-mode-block-padding tab-width
      web-mode-enable-auto-indentation nil
      web-mode-enable-auto-pairing nil)
```


<a id="orgfd8a4a7"></a>

### YAML

YAML configurationf files are highlighted by `yaml-mode`.

```elisp
(td/auto-mode '(("\\.yml\\'" . yaml-mode)))
```


<a id="org2e120c6"></a>

### Yasnippet

Snippets are provided by the `yasnippet` and `yasnippet-snippets` packages.

```elisp
(with-eval-after-load 'yasnippet
  (global-set-key (kbd "C-c ,") #'yas-expand)
  (setq yas-snippet-dirs '("~/.config/emacs/yasnippets"))
  (yas-reload-all))

(add-hook 'prog-mode-hook #'yas-minor-mode)
(add-hook 'text-mode-hook #'yas-minor-mode)
```


<a id="org9dea35e"></a>

### Load Customizer Settings

Load the file we created for custom vars in the [general settings](#orgf4648c1).

```elisp
(load custom-file 'noerror 'nomessage)
```


<a id="orgc0c7a1c"></a>

## Installation

If you've decided to fork this repository and wish to use it as-is, here are the steps you'll need to take.

1.  Clone this repository somewhere.
2.  Tangle `config.org`. The resulting configuration files should be output to `.emacs.d/*.el`
3.  Symlink, copy or move the config files to wherever you want to start your init.


<a id="org9bdc172"></a>

## Licenses

-   For the [bell sound](inspectorj_bell.wav): "Bell, Candle Damper, A (H4n).wav" by InspectorJ (www.jshaw.co.uk) of Freesound.org (Creative Commons - CC BY 3.0)


<a id="org24d4bcf"></a>

## Closing Thoughts

This literate configuration is a labour of love from a man who changes his mind and mixes things up *often*.

I'm not sure it will ever be finished or perfect. At times, things may clunk. I will do my best to clunk them in another branch.

If you like this config the way you found it, make sure that you fork it or make note of which commit you preferred.

If you like it enough to drop me a tip, feel free to do so:

[![img](https://ko-fi.com/img/githubbutton_sm.svg)](https://ko-fi.com/Y8Y34UWHH) [![img](https://liberapay.com/assets/widgets/donate.svg)](https://liberapay.com/trev.dev/donate) BTC: bc1qwad2jlteldw644w4wfh28y6ju53zfp69nnswrq
